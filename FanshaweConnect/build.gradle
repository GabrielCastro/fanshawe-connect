/*
 * This file is part of FanshaweConnect.
 *
 * Copyright 2013 Gabriel Castro (c)
 *
 *     FanshaweConnect is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 2 of the License, or
 *     (at your option) any later version.
 *
 *     FanshaweConnect is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with FanshaweConnect.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.android.support:appcompat-v7:19.0.+'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile "ch.acra:acra:4.5.0"
    compile 'com.readystatesoftware.systembartint:systembartint:+'

    compile files('libs/non_free/ga/libGoogleAnalyticsServices.jar')

    compile project(':libraries:ntlm')
    compile project(':BetterPreferences')
}

def getUri() {
    if (project.hasProperty("acra_uri")) {
        return acra_uri;
    }
    return "null"
}

def getUsr() {
    if (project.hasProperty("acra_usr")) {
        return acra_usr;
    }
    return "null"
}

def getPwd() {
    if (project.hasProperty("acra_pwd")) {
        return acra_pwd;
    }
    return "null"
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.1'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }

    productFlavors {
        non_free {
            buildConfigField "String", "ACRA_URI", getUri()
            buildConfigField "String", "ACRA_USR", getUsr()
            buildConfigField "String", "ACRA_PWD", getPwd()
            buildConfigField "boolean", "ACRA_ENABLE", "true"
        }

        free {
            buildConfigField "String", "ACRA_URI", '"ACRA-NULL-STRING"'
            buildConfigField "String", "ACRA_USR", '"ACRA-NULL-STRING"'
            buildConfigField "String", "ACRA_PWD", '"ACRA-NULL-STRING"'
            buildConfigField "boolean", "ACRA_ENABLE", "false"
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    // ~/.gradle/gradle.properties
    if (
    project.hasProperty('fan_keystoreFile') &&
            project.hasProperty('fan_keystorePassword') &&
            project.hasProperty('fan_keystoreAliasPassword')
    ) {
        android.signingConfigs.release.storeFile = file(fan_keystoreFile)
        android.signingConfigs.release.storePassword = fan_keystorePassword
        android.signingConfigs.release.keyPassword = fan_keystoreAliasPassword
        android.signingConfigs.release.keyAlias = fan_keystoreAlias
    } else {
        android.buildTypes.release.signingConfig = null
    }

}
